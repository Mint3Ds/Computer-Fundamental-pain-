// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    	DMux(in=true, sel=instruction[15], a=ains, b=cins);
        Or(a=ains, b=instruction[5], out=intoA);
        And(a=cins, b=instruction[4], out=intoD);
	And(a=instruction[15],b=instruction[12],out=AorM);

        Mux16(a=outtM, b=instruction, sel=ains, out=ToA);
        ARegister(in=ToA, load=intoA, out=A, out[0..14]=addressM);

 	Mux16(a=A, b=inM, sel=AorM, out=AM);
	
	ALU(x=D,y=AM,zx=instruction[11],nx=instruction[10],zy=instruction[9],ny=instruction[8],f=instruction[7],no=instruction[6],out=outtM,out=outM,zr=zr,	ng=ng);

	DRegister(in=outtM,load=intoD,out=D);

	And(a=instruction[15],b=instruction[3],out=writeM);

	Nor(a=ng,b=zr,out=posnzr);
	And(a=instruction[0],b=posnzr,out=ld1);//Jump if greater than

	And(a=instruction[1],b=zr,out=ld2);//Jump if Equal


	And(a=instruction[2],b=ng,out=ld3);//Jump if less than


	Or3Way(a=ld1,b=ld2,c=ld3,out=ld);
	And(a=instruction[15],b=ld,out=RealJump);

	PC(in=A,load=RealJump,inc=true,reset=reset,out[0..14]=pc);
	

	

}